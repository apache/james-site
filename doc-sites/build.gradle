plugins {
  id 'base'
  id 'com.github.node-gradle.node' version '2.2.4'
  id 'org.ajoberstar.grgit' version '4.0.2'
  id 'org.ajoberstar.git-publish' version '2.1.3'
}

node {
  // Version of node to use.
  version = '12.18.2'
  download = true
}

def siteOutputDir = "$buildDir/site"

gitPublish {
    repoDir = file(siteOutputDir)
    branch = 'asf-staging-test'
}

task generateDocs(type: NpxTask) {
  dependsOn npmInstall
  // dependsOn gitPublishReset

  inputs.files('package.json', 'package-lock.json', 'antora-playbook.yml')
  inputs.dir(fileTree('node_modules').exclude('.cache'))
  outputs.dir(gitPublish.repoDir)

  command = 'antora'
  args = ['antora-playbook.yml', '--to-dir', siteOutputDir ]
}

task copySomeFiles(type: Copy) {
  dependsOn generateDocs
  from 'src/main/asf'
  into siteOutputDir
}

task buildSite() {
  dependsOn generateDocs, copySomeFiles
}

tasks.build.dependsOn buildSite
tasks.gitPublishCommit.dependsOn buildSite